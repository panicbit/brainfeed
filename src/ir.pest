ir = _{ SOI ~ (stmt)* ~ EOI }

stmt = {
	  stmt_decl
	| stmt_assign
    | stmt_add_assign
    | stmt_sub_assign
    | stmt_while
    | stmt_if
}
stmt_decl = { "let" ~ ident ~ ("=" ~ expr)? }
stmt_assign = { ident ~ "=" ~ expr }
stmt_add_assign = { ident ~ "+=" ~ expr }
stmt_sub_assign = { ident ~ "-=" ~ expr }
stmt_while = { "while" ~ expr ~ "{" ~ stmt* ~ "}" }
stmt_if = { "if" ~ expr ~ "{" ~ stmt* ~ "}" }

expr = { term ~ (op ~ term)* }
expr_const = { number }
expr_var = { ident }
expr_char = { "'" ~ char ~ "'" }

term = _{
	  expr_const
    | expr_var
    | expr_char
    | "(" ~ expr ~ ")"
}
op = _{
	  op_add
    | op_sub
    | op_gt
}
op_add = { "+" }
op_sub = { "-" }
op_gt = { ">" }

number = @{ ASCII_DIGIT+ }
ident = @{ ASCII_ALPHA ~ (ASCII_ALPHA | "_")* }
char = @{ ASCII_ALPHA }

WHITESPACE = _{ " " | "\n" | "\t" }
